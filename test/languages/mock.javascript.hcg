# Mock version of JavaScript with comments and automatic semicolon "insertion". 
# This is NOT a complete example of the JavaScript specification

@IGNORE g:ws g:nl tk:comments


<> javascript > statements                                                          f:r{ { ty:"js", stmts:$1 } }


<> statements > ( statement | declaration )(+)


<> statement > for_statement
    | block_statement
    | expression_statement
    | return_statement
    | empty_statement

<> empty_statement > \;

<> for_statement > t:for \( var_expression \; expression \; expression \) statement
                                                                                  f:r{ { ty:"for", init:$3, compare:$5, iter:$7, stmt:$9 } }

<> return_statement > \return asi
    | \return expression asi


<> block_statement > { statements }                                               f:r{ { ty:"block", statements:$2 } }


<> expression_statement > expression_list asi                                     f:r{ { ty:"expression_statement", exprs:$1 } }


<> declaration > function_declaration
    | var_declaration                                                             


<> function_declaration > \function g:id \( g:id(*\, ) \) { statements? }   
                                                                                f:r{ { ty:"fn", id:$2, args:$$4, statements:$7 } }


<> var_declaration > var_expression  asi                         
                                                                                f:r{ { ty:"var", assignments:$1 } }

<> var_expression > \var assignment_expression(+\, )
                                                                                f:r{ { ty:"var", assignments:$2 } }

<> expression_list > expression(+\, )


<> expression > additive_expression
    | assignment_expression


<> assignment_expression > id \= expression                                        
                                                                                f:r{ { ty: "assignment", left:$1, right:$3 } }

<> additive_expression > multiplicative_expression ( \+ | \- ) additive_expression   
                                                                                f:r{ { ty: $2 == "+" ?  "add" : "sub", left:$1, right:$3 } }
    | multiplicative_expression

<> multiplicative_expression > unary_expression ( \* | \/ ) multiplicative_expression
                                                                                f:r{ { ty: $2 == "*" ?  "mul" : "div", left:$1, right:$3 } }
    | unary_expression


<> unary_expression > g:num                                                     f:r{ { ty:"num", val:$1 } }
    | id
    | \( expression \)                                                          f:r{ { ty:"parenthesized", val:$2 } }

<> id > g:id                                                                    f:r{ { ty:"id", val:$1 } }


<> asi > \; | ?={ | ?=g:nl | ?=$eof

<> comments > \/* ( g:ws | g:nl | g:id | g:num | g:sym )(*) \*/
    | \// ( g:ws | g:id | g:num | g:sym )(*) g:nl