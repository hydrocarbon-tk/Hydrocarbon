# Copyright (C) 2021 Anthony Weathersby - The Hydrocarbon Parser Compiler
# see /source/typescript/hydrocarbon.ts for full copyright and warranty 
# disclaimer notice.

# version 4.0

#
# continued from ./hcg.hcg
#
# Imported files can have their own imports. Hydrocarbon automatically resolves 
# relative references and deduplicate imports.

@IMPORT ./comments.hcg as cm

@IMPORT ./symbols.hcg as sym

@IGNORE g:sp g:nl

<> hcg_preamble > preamble

# Repetition of symbols can be achieved through appending `(*)` or `(+)` to the end
# of the symbol.  `(*)` indicates zero or more iterations of the symbol should occur, 
# and `(+)` indicates one or more iterations should occur. 

<> preamble > preamble_clause(*)

# Separating symbols can also be defined through `(+ <separator-symbols>)` or 
# `(* <separator-symbols>)`. 
# For example:
# `g:num(*,)` would match inputs of one or more series integers separated by a comma, 
# such as `1,2,3,4,5,6,7,8,9`, `123456789`, or `12,345,6789`.

# TODO: Detailed annotation on production body separator `|`

<> preamble_clause >  ignore_preamble
    | import_preamble
    | cm::comment


# Production bodies, the symbols defined after `<> <production-name> >` can contain a mixture of 
# non-terminal production names, terminal symbols, and meta statements such as "(ERR g:sp)"

# In either the Constructor Reduce function or the Return Expression Reduce function
# the symbols of the production can be addressed using the $ operator, which resolves
# to the index location of the terminal symbol or non-terminal production within the 
# production body. In the following case, $3 resolves to the third symbol in the proceeding
# precedence_preamble body, sym::terminal_symbol ( keep in mind [(ERR g:sp)] is a meta object 
# and is not an actual symbol of the production body). $4 resolves to the g:num symbol.
                                                  

<> ignore_preamble >         \@IGNORE sym::ignore_symbols g:nl                
                                                                        
                                                                        f:r { { type:"ignore", symbols : $3 } }


<> import_preamble >         \@IMPORT g:sp(*) ( g:id | g:sym  )(+\" ) g:sp(+) ( t:AS | t:as ) sym::identifier g:nl  
                                                                        
                                                                        f:r { { type:"import", uri: $4, reference:$7 } } 

<> export_preamble >         \@EXPORT ( sym::imported_production_symbol | sym::production_symbol )(+) 
                                                                        
                                                                        f:r { { type:"export", production_names: $2, } } 


